# ============================================
# Zsh Configuration
# ============================================

# -- Profiling (optional) --------------------
# zmodload zsh/zprof  # Uncomment to enable startup profiling (slows startup)
# zprof               # Uncomment at the bottom to view profiling results

# ============================================
# Exit early for non-interactive shells
# ============================================
case $- in
  *i*) ;;   # interactive
  *) return ;;
esac
[ -z "$PS1" ] && return

# ============================================
# Paths & Fpath Setup
# ============================================

# Add custom completions
if [ -d "$HOME/.config/zsh/completions" ]; then
  fpath+=("$HOME/.config/zsh/completions")
fi

# Remove duplicates to speed up search
builtin declare -aU fpath path manpath
builtin declare +x FPATH  # FPATH should not be exported
fpath=(${(u)fpath})
path=(${(u)path})
manpath=(${(u)manpath})

# ============================================
# Emulation Checks
# ============================================
case "$0" in
  ksh|sh)   source "$HOME/.shrc" && return ;;
  bash)     source "$HOME/.bashrc" && return ;;
esac

unset _myPaths _path

# ============================================
# Ensure XDG Directories Exist
# ============================================
xdg_dirs=(
  "$HOME/.cache/zsh"
  "$HOME/.local/share/zsh"
  "$HOME/.local/state/zsh"
)
for dir in $xdg_dirs; do
  [ -d "$dir" ] || mkdir -p "$dir"
done

# ============================================
# Completions Paths
# ============================================
if [ -d "$HOME/.local/share/zsh/completions" ]; then
  fpath+=("$HOME/.local/share/zsh/completions")
fi
if [ -d "$HOME/.local/share/zsh/zsh-completions/src" ]; then
  fpath+=("$HOME/.local/share/zsh/zsh-completions/src")
fi
if type brew &>/dev/null; then
  fpath+=("/opt/homebrew/share/zsh/site-functions")
fi

# ============================================
# Initialize Completions
# ============================================
autoload -Uz compinit
zcdump="$HOME/.cache/zsh/zcompdump-${ZSH_VERSION}"

# Compile if needed
if [[ -r "$zcdump" && (! -f "${zcdump}.zwc" || "$zcdump" -nt "${zcdump}.zwc") ]]; then
  zcompile "$zcdump"
fi

# Use cache if fresh (<24h), else rebuild
if [[ -r "$zcdump" && -n "$zcdump"(#qN.mh-24) ]]; then
  compinit -C -d "$zcdump"
else
  compinit -d "$zcdump"
fi

# ============================================
# Source Config Files
# ============================================
config_dirs=("${ZDOTDIR:-$HOME/.config/zsh}/conf.d")
for dir in "${config_dirs[@]}"; do
  [ -d "$dir" ] || continue
  for file in "$dir"/*.{zsh,sh}(.N) "$dir"/*/*.{zsh,sh}(.N); do
    [[ -n "$ZSH_DEBUG" ]] && echo "Loading config file: $file" >&2
    source "$file"
  done
done

# zprof               # Uncomment at the bottom to view profiling results
