# Exclude ~/Library/Caches/antidote/ in Clean MyMac to prevent deletions
# https://arc.net/l/quote/ejwiyzxh

# ======================================================
# Antidote Plugin Manager Setup
# ======================================================
#
# Startup path:
# 1) Source the static plugin bundle (.zsh) directly.
# 2) Only load Antidote and regenerate bundle when needed.
# This avoids paying Antidote initialization cost on every shell startup.

zsh_plugins=${ZDOTDIR:-$HOME/.config/zsh}/.zsh_plugins
zsh_plugins_txt=${zsh_plugins}.txt
zsh_plugins_zsh=${zsh_plugins}.zsh
zsh_plugins_zwc=${zsh_plugins}.zwc
zsh_config_root=${ZDOTDIR:-$HOME/.config/zsh}
antidote_script=""

[[ -f "$zsh_plugins_txt" ]] || touch "$zsh_plugins_txt"

if [[ ! -s "$zsh_plugins_zsh" || ! "$zsh_plugins_zsh" -nt "$zsh_plugins_txt" ]]; then
  if [[ -r "${zsh_config_root}/.antidote/antidote.zsh" ]]; then
    antidote_script="${zsh_config_root}/.antidote/antidote.zsh"
{{- if eq .chezmoi.os "darwin" }}
  elif [[ -r "/opt/homebrew/opt/antidote/share/antidote/antidote.zsh" ]]; then
    antidote_script="/opt/homebrew/opt/antidote/share/antidote/antidote.zsh"
{{- else if and (eq .chezmoi.os "linux") (eq .chezmoi.osRelease.id "arch") }}
  elif [[ -r "/usr/share/zsh-antidote/antidote.zsh" ]]; then
    antidote_script="/usr/share/zsh-antidote/antidote.zsh"
{{- end }}
  fi

  if [[ -n "$antidote_script" ]]; then
    source "$antidote_script"
    antidote bundle <"$zsh_plugins_txt" >|"$zsh_plugins_zsh"

    # Compile once after regeneration; zsh will auto-pick .zwc when valid.
    if [[ ! -f "$zsh_plugins_zwc" || "$zsh_plugins_zsh" -nt "$zsh_plugins_zwc" ]]; then
      zcompile "$zsh_plugins_zsh"
    fi
  else
    print -u2 "warning: antidote.zsh not found; skipping plugin bundle regeneration"
  fi
fi

[[ -r "$zsh_plugins_zsh" ]] && source "$zsh_plugins_zsh"

unset antidote_script zsh_plugins zsh_plugins_txt zsh_plugins_zsh zsh_plugins_zwc zsh_config_root
